apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
apply from: 'stable-Ids.gradle'
apply from: 'tasks.gradle'

apply plugin: 'ihandy.proguard.rewrite'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    aaptOptions {
        ignoreAssetsPattern "!*.plist:!.DS_Store"
    }

    signingConfigs {
        release {
            keyAlias 'colorphone'
            keyPassword 'zdhszyzs'
            storeFile file('keystore/ColorPhone.jks')
            storePassword 'zdhszyzs'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    flavorDimensions "mode"

    productFlavors {
        colorphone {
            dimension "mode"
            applicationId "com.colorphone.smooth.dialer"
            versionCode 44
            versionName "1.3.2"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'src/colorphone/proguard-rules.pro'

        }
        colorflash {
            dimension "mode"
            applicationId "com.app.phone.call.flash.screen"
            versionCode 54
            versionName "1.2.4"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'src/colorflash/proguard-rules.pro'

        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/main/res',
                           'src/main/java/com/honeycomb/colorphone/telecomeventui/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/notification/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/lettertile/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/commontheme/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/impl/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/contactgrid/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/dialpadview/res',
                           'src/main/java/com/honeycomb/colorphone/dialer/theme/res',

                           'src/main/java/com/honeycomb/colorphone/dialer/res']
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
        }
        colorphone {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/colorphone/res', 'src/main/res']
            assets.srcDirs = ['src/colorphone/assets', 'src/main/assets']
            java.srcDirs = ['src/colorphone/java', 'src/main/java']
        }
        colorflash {
            manifest.srcFile 'src/colorflash/AndroidManifest.xml'
            res.srcDirs = ['src/colorflash/res', 'src/main/res']
            assets.srcDirs = ['src/colorflash/assets', 'src/main/assets']
            java.srcDirs = ['src/colorflash/java', 'src/main/java']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }

        debug {
            versionNameSuffix ".develop"
        }
    }
    lintOptions {

        abortOnError false
    }
    ihandyProGuard {
        packageNames = ['com.acb.call',
                        'com.call.assistant',
                        'com.superapps',
                        'colorphone.acb.com']
        packageNameMap = [
                "colorflash":['cfl'],
                "colorphone":['cph']
        ]
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

//    splits {
//        abi {
//            enable true
//            reset()
//            include "armeabi"
//            universalApk false
//        }
//    }
}

final SUPPORT_LIBRARY_VERSION = '26.1.0'
final GOOGLE_PLAY_SERVICES_VERSION = '15.0.0'

dependencies {
    compile('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

//    debugCompile 'com.amitshekhar.android:debug-db:1.0.3'

    final USE_BETA_ACB_LIBS = false

    final COMMON_DEPENDENCIES = [
            // ==== Support ====
            "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:design:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support.constraint:constraint-layout:1.0.2",

            "com.android.support:multidex:1.0.1",

            "com.google.android.gms:play-services-gcm:$GOOGLE_PLAY_SERVICES_VERSION".toString(),
            "com.google.android.gms:play-services-ads:$GOOGLE_PLAY_SERVICES_VERSION".toString(),

            'com.google.firebase:firebase-core:16.0.3',
            'com.google.firebase:firebase-messaging:17.3.0',
//            USE_BETA_ACB_LIBS ? "net.appcloudbox.autopilot:libAutoPilot.beta:5.5.0.+"
//                    : "net.appcloudbox.autopilot:libAutoPilot.gm:5.5.0.gm",
            'net.appcloudbox.autopilot:libAutoPilot.gm:5.5.2.gm',
            'com.ihandy.appframework:libAppframework.gm:4.12.0.gm',
    ]

    for (dependency in COMMON_DEPENDENCIES) compile dependency

    // ==== Third part ====
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    // Fix crash on Android O background service
    compile 'com.liulishuo.filedownloader:library:1.7.6-SNAPSHOT'
    compile('com.airbnb.android:lottie:2.5.5') {
        transitive = false;
    }
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.7'
    compile 'jp.wasabeef:recyclerview-animators:2.2.7'
    compile 'eu.davidea:flexible-adapter:5.0.0-rc1'
    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // ==== ihandy ====
    // ------ Permanent ------
    final permanentVersion = ".gm:4.0.0.gm"
    compile "com.ihandy.permanent:permanent$permanentVersion" // 保活主模块
    compile "com.ihandy.permanent:native-guard$permanentVersion" // native保活模块，需要才加

    // ------ Submodule ------
    compile files('../libs/libFlashlight/libFlashlight.jar')

    compile project(':libCallAssistant')
    compile project(':libMessageCenter')
    compile project(':libScreenFlash')
    compile project(':libSuperApps')
    compile project(':permissions')
    compile project(':libWeather')
    //  -------- GoldEye ------
    colorphoneCompile 'net.appcloudbox.goldeneye:colorphone_a:2019.06.04.001'
    colorflashCompile 'net.appcloudbox.goldeneye:colorflash_a:2019.06.04.001'

}

android.applicationVariants.all { variant ->
    tasks.named("generate${variant.name.capitalize()}Resources").configure { mergeResourceTask ->
        mergeResourceTask.outputs.upToDateWhen { false }
    }
    tasks.named("merge${variant.name.capitalize()}Resources").configure { mergeResourceTask ->
        mergeResourceTask.outputs.upToDateWhen { false }
    }

    def taskMiddleName = variant.variantData.name.capitalize()
    def flavor = variant.flavorName
    // For all launcher builds: perform plist -> pa conversion before merge{Variant}Assets tasks
    Task mergeAssets = tasks.findByName("merge${taskMiddleName}Assets")
    Task encodePlistArchives = tasks.create(name: "encode${taskMiddleName}PlistArchives",
            type: Exec,
            group: 'Color Phone',
            description: 'Perform plist -> pa conversion before merge{Variant}Assets tasks.') {
        commandLine '../plist-conv.sh', flavor
        mergeAssets.outputs.upToDateWhen { false }
        // Clear mergeAssets task UP-TO-DATE status to force a re-run
    }
    mergeAssets.dependsOn encodePlistArchives

}
// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
