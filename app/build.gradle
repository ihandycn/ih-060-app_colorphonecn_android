apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'
apply from: 'stable-Ids.gradle'
apply from: 'tasks.gradle'

apply plugin: 'ihandy.proguard.rewrite'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias 'colorphone'
            keyPassword 'zdhszyzs'
            storeFile file('keystore/ColorPhone.jks')
            storePassword 'zdhszyzs'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    flavorDimensions "mode"

    productFlavors {
        colorphone {
            dimension "mode"
            applicationId "com.colorphone.smooth.dialer"
            versionCode 44
            versionName "1.3.2"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'src/colorphone/proguard-rules.pro'

        }
        colorflash {
            dimension "mode"
            applicationId "com.app.phone.call.flash.screen"
            versionCode 47
            versionName "1.1.9"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
                    'src/colorflash/proguard-rules.pro'

        }
    }

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
            java.srcDirs = ['src/main/java']
        }
        colorphone {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = ['src/colorphone/res', 'src/main/res']
            assets.srcDirs = ['src/colorphone/assets', 'src/main/assets']
            java.srcDirs = ['src/colorphone/java', 'src/main/java']
        }
        colorflash {
            manifest.srcFile 'src/colorflash/AndroidManifest.xml'
            res.srcDirs = ['src/colorflash/res', 'src/main/res']
            assets.srcDirs = ['src/colorflash/assets', 'src/main/assets']
            java.srcDirs = ['src/colorflash/java', 'src/main/java']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
        }

        debug {
            versionNameSuffix ".develop"
        }
    }
    lintOptions {

        abortOnError false
    }
    ihandyProGuard {
        packageNames = ['com.acb.call',
                        'com.call.assistant',
                        'com.ihs.chargingreport',
                        'com.superapps',
                        'colorphone.acb.com']
        packageNameMap = [
                "colorflash":['cfl'],
                "colorphone":['cph']
        ]
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

//    splits {
//        abi {
//            enable true
//            reset()
//            include "armeabi"
//            universalApk false
//        }
//    }
}

final SUPPORT_LIBRARY_VERSION = '26.1.0'
final GOOGLE_PLAY_SERVICES_VERSION = '15.0.0'

dependencies {
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }

//    debugCompile 'com.amitshekhar.android:debug-db:1.0.3'

    final USE_BETA_ACB_LIBS = false

    final COMMON_DEPENDENCIES = [
            // ==== Support ====
            "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:design:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION".toString(),
            "com.android.support.constraint:constraint-layout:1.0.2",

            "com.android.support:multidex:1.0.1",

            "com.google.android.gms:play-services-gcm:$GOOGLE_PLAY_SERVICES_VERSION".toString(),
            "com.google.android.gms:play-services-ads:$GOOGLE_PLAY_SERVICES_VERSION".toString(),

            'com.google.firebase:firebase-core:16.0.3',
            'com.google.firebase:firebase-messaging:17.3.0',
//            USE_BETA_ACB_LIBS ? "net.appcloudbox.autopilot:libAutoPilot.beta:5.5.0.+"
//                    : "net.appcloudbox.autopilot:libAutoPilot.gm:5.5.0.gm",
            "net.appcloudbox.autopilot:libAutoPilot.gm:5.5.2.gm",

    ]

    final  COMMON_COLORPHONE = [
            USE_BETA_ACB_LIBS ?
                    'com.ihandy.appframework:libAppframework.beta:4.9.0.+' :
                    'com.ihandy.appframework:libAppframework.gm:4.9.0.gm',
    ]

    final  COMMON_COLORFLASH = [
            USE_BETA_ACB_LIBS ?
                    'com.ihandy.appframework:libAppframework.beta:4.9.0.+' :
                    'com.ihandy.appframework:libAppframework.gm:4.9.0.gm',
    ]


    for (dependency in COMMON_DEPENDENCIES) compile dependency

    for (dependency in COMMON_COLORPHONE) colorphoneCompile dependency
    for (dependency in COMMON_COLORFLASH) colorflashCompile dependency


    // ==== Third part ====
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'
    // Fix crash on Android O background service
    compile 'com.liulishuo.filedownloader:library:1.7.6-SNAPSHOT'
    compile ("com.airbnb.android:lottie:2.5.5") {
        transitive = false;
    }

    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.7'
    compile 'jp.wasabeef:recyclerview-animators:2.2.7'
    compile "eu.davidea:flexible-adapter:5.0.0-rc1"

    // LeakCanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // ==== ihandy ====
    final LIB_DEVICE_VERSION = '.gm:4.5.3.gm'
    compile "com.ihandy.device:device-clean-memory$LIB_DEVICE_VERSION".toString()
    compile "com.ihandy.device:device-monitor-app-usage$LIB_DEVICE_VERSION".toString()
    compile "com.ihandy.device:device-monitor-top-app$LIB_DEVICE_VERSION".toString()
    compile "com.ihandy.device:device-permanent$LIB_DEVICE_VERSION".toString()

    // Submodule
    compile files('../libs/libFlashlight/libFlashlight.jar')

    compile project(':libCallAssistant')
    compile project(':libMessageCenter')
    compile project(':libScreenFlash')
    compile project(':libSuperApps')
    compile project(':permissions')

    //  -------- GoldEye ------
    colorphoneCompile 'net.appcloudbox.goldeneye:colorphone_a:2019.02.12.001'
    colorflashCompile 'net.appcloudbox.goldeneye:colorflash_a:2019.02.13.001'
}
android.applicationVariants.all { variant ->
    tasks.named("generate${variant.name.capitalize()}Resources").configure { mergeResourceTask ->
        mergeResourceTask.outputs.upToDateWhen { false }
    }
    tasks.named("merge${variant.name.capitalize()}Resources").configure { mergeResourceTask ->
        mergeResourceTask.outputs.upToDateWhen { false }
    }
}
// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'
